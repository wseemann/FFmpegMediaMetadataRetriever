apply plugin: 'com.android.library'
apply from: "$rootProject.projectDir/publishing.gradle"

android {
    namespace 'wseemann.fmmr.core'
    compileSdk versions.get("compileSdkVersion")

    defaultConfig {
        minSdkVersion versions.get("minSdkVersion")
        targetSdkVersion versions.get("targetSdkVersion")
        versionCode versions.get("versionCode")
        versionName versions.get("versionName")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    publishing {
        singleVariant('release')
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.wseemann'
                artifactId = 'FFmpegMediaMetadataRetriever-core'
                version = "$project.android.defaultConfig.versionName"

                pom {
                    name = 'FFmpegMediaMetadataRetriever-core'
                    description = 'FFmpegMediaMetadataRetriever library'
                    url = 'https://github.com/wseemann/FFmpegMediaMetadataRetriever'
                    packaging = 'aar'
                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'wseemann'
                            name = 'William Seemann'
                        }
                    }
                    scm {
                        connection = 'scm:hg:https://github.com/wseemann/FFmpegMediaMetadataRetriever'
                        developerConnection = 'scm:hg:https://github.com/wseemann/FFmpegMediaMetadataRetriever'
                        url = 'https://github.com/wseemann/FFmpegMediaMetadataRetriever'
                    }
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
